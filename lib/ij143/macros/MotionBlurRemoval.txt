  // Demonstrates motion blur removal using deconvolution.
  // For more information, do a Google search for "FFT motion blur".

  // open the "bridge" sample image
  run("Bridge (174K)");

  // Create a line-shaped point source function
  newImage("PSF", "8-bit black", 512, 512, 1);
  setForegroundColor(255, 255, 255);
  makeLine(251, 251, 261, 261);
  run("Draw");
  run("Select None");

  // Convolve Bridge with PSF to generate motion blurred image
  run("FD Math...", "image1=bridge.gif operation=Convolve image2=PSF result='Motion Blurred' do");

  // Deconvolve motion blurred image with PSF to generate deblurred image
  run("FD Math...", "image1='Motion Blurred' operation=Deconvolve image2=PSF result=Deblurred do");

